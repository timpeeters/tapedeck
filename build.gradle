plugins {
    id "checkstyle"
    id "jacoco"
    id "java"
    id "pmd"
}

defaultTasks 'build'

jacoco {
    toolVersion = '0.8.5'
}

allprojects {
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'

    repositories {
        jcenter()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'jacoco'
    apply plugin: 'java'

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    checkstyle {
        ignoreFailures = true
        toolVersion = '8.30'
    }

    dependencies {
        constraints {
            implementation 'org.assertj:assertj-core:3.16.0'
            implementation 'org.mockito:mockito-core:3.3.3'
            implementation 'org.yaml:snakeyaml:1.26'
        }

        implementation platform('org.junit:junit-bom:5.6.2')
        implementation platform('org.springframework.boot:spring-boot-dependencies:2.2.7.RELEASE')
    }

    pmd {
        consoleOutput = true
        ignoreFailures = true
        ruleSetFiles = files("${rootProject.projectDir}/config/pmd/ruleset.xml")
        ruleSets = []
        toolVersion = '6.21.0'
    }

    test {
        useJUnitPlatform()
    }
}

task codeCoverageReport(type: JacocoReport) {
    dependsOn = subprojects.build

    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled = false
        csv.enabled = false
    }
}

rootProject.check.dependsOn codeCoverageReport
